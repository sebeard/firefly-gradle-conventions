plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
    id 'com.github.jmongard.git-semver-plugin'
    id 'org.owasp.dependencycheck'
    id 'com.github.ben-manes.versions'
    id 'org.sonarqube'
}

repositories {
    gradlePluginPortal()
}

group = 'io.firefly'
version semver.version

semver {
    releaseTagNameFormat = "v%s"
}

dependencies {
    implementation group: 'com.avast.gradle.docker-compose', name: 'com.avast.gradle.docker-compose.gradle.plugin', version: "${dockerComposePluginVersion}"
    implementation group: 'com.github.ben-manes.versions', name: 'com.github.ben-manes.versions.gradle.plugin', version: "${versionsPluginVersion}"
    implementation group: 'com.github.spotbugs', name: 'com.github.spotbugs.gradle.plugin', version: "${spotbugsPluginVersion}"
    implementation group: 'gradle.plugin.com.hierynomus.gradle.plugins', name: 'license-gradle-plugin', version: "${licensePluginVersion}"
    implementation group: 'gradle.plugin.com.github.jmongard.git-semver-plugin', name: 'git-semver-plugin', version: "${semanticVersioningPluginVersion}"
    implementation group: 'info.solidsoft.pitest', name: 'info.solidsoft.pitest.gradle.plugin', version: "${pitestPluginVersion}"
    implementation group: 'nebula.dependency-recommender', name: 'nebula.dependency-recommender.gradle.plugin', version: "${dependencyRecommenderVersion}"
    implementation group: 'net.linguica.maven-settings', name: 'net.linguica.maven-settings.gradle.plugin', version: "${linguicaVersion}"
    implementation group: 'org.owasp.dependencycheck', name: 'org.owasp.dependencycheck.gradle.plugin', version: "${dependencyCheckPluginVersion}"
    implementation group: 'org.sonarqube', name: 'org.sonarqube.gradle.plugin', version: "${sonarqubePluginVersion}"

    implementation group: 'io.spring.dependency-management', name: 'io.spring.dependency-management.gradle.plugin', version: "${springDependencyPluginVersion}"
    implementation group: 'org.springframework.boot', name: 'org.springframework.boot.gradle.plugin', version: "${springBootPluginVersion}"
    implementation group: 'org.springframework.boot.experimental.thin-launcher', name: 'org.springframework.boot.experimental.thin-launcher.gradle.plugin', version: "${springBootThinLayoutPluginVersion}"
    implementation group: 'com.github.johnrengelman.shadow', name: 'com.github.johnrengelman.shadow.gradle.plugin', version: "${shadowPluginVersion}"
}

dependencyCheck {
    failOnError = false
    format = 'ALL'
    cveValidForHours = 4
    suppressionFile = "$project.rootDir/suppressions.xml"
}
check.dependsOn dependencyCheckAggregate

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
    checkConstraints = true
}
build.dependsOn dependencyUpdates

sonarqube {
    properties {
        property "sonar.projectKey", "gradle-conventions"
        property "sonar.projectName", "gradle-conventions"
        property "sonar.exclusions", "**/build/generated/**,"
        property "sonar.coverage.exclusions", "**/src/test/**"
        property "sonar.coverage.jacoco.xmlReportPaths", ["${project.buildDir}/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"]
        property "sonar.dependencyCheck.reportPath", "$project.buildDir/reports/dependency-check-report.xml"
        property "sonar.dependencyCheck.htmlReportPath", "$project.buildDir/reports/dependency-check-report.html"
        property "dc5.mutationAnalysis.pitest.sensor.reports.directory", "build/reports/pitest"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sebeard/firefly-packages")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_PACKAGES_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId project.group
            from components.java
        }
    }
}

tasks.named('publish') {
    dependsOn('check')
}
